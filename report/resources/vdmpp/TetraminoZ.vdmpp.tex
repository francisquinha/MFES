\begin{vdmpp}[breaklines=true]
class TetraminoZ is subclass of Tetramino
  
 -- Tetramino of the type Z: xx
 --        xx
 --
 -- Minoes:      12
 --          34
 

 operations
 
(*@
\label{TetraminoZ:12}
@*)
  public TetraminoZ : (Game) ==> TetraminoZ
  TetraminoZ(game) == (
   Tetramino`setColor(<Red>);
   Tetramino`setId(7);
   Tetramino`initialSetMinoes(game, [1, 4]);
   return self
  );

(*@
\label{getNextMino:20}
@*)
  protected getNextMino: Board`Position * nat ==> Board`Position
  getNextMino(position, index) == (
   dcl result : Board`Position := position;
   cases Tetramino`getOrientation():
    0 -> (
     cases index:
      2 -> result(1) := position(1) + 1,
      others -> result(2) := position(2) + 1
     end
    ),
    1 -> (
     cases index:
      2 -> result(2) := position(2) - 1,
      others -> result(1) := position(1) + 1
     end
    ),
    2 -> (
     cases index:
      2 -> result(1) := position(1) - 1,
      others -> result(2) := position(2) - 1
     end
    ),
    3 -> (
     cases index:
      2 -> result(2) := position(2) + 1,
      others -> result(1) := position(1) - 1
     end
    )
   end;
   return result
  )
  pre index in set {1, ..., 4} 
   and Tetramino`checkPosition(position, 1, Board`maxRow, 1, Board`maxColumn)
  post Tetramino`checkPosition(RESULT, 0, Board`maxRow + 1, 0, Board`maxColumn + 1);
   
(*@
\label{getRotatedMino:55}
@*)
  protected getRotatedMino: Board`Position ==> Board`Position
  getRotatedMino(position) == (
   dcl result : Board`Position := position;
   cases Tetramino`getOrientation():
    0 -> result(2) := position(2) + 2,
    1 -> result(1) := position(1) + 2,
    2 -> result(2) := position(2) - 2,
    3 -> result(1) := position(1) - 2
   end;
   return result
  )
  pre Tetramino`checkPosition(position, 1, Board`maxRow, 1, Board`maxColumn)
  post Tetramino`checkPosition(RESULT, -1, Board`maxRow + 2, -1, Board`maxColumn + 2);

end TetraminoZ
\end{vdmpp}
