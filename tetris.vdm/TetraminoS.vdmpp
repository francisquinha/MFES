class TetraminoS is subclass of Tetramino
		
	operations
		public TetraminoS : (Game) ==> TetraminoS
		TetraminoS(game) == (
			Tetramino`setColor(<Green>);
			Tetramino`setId(5);
			Tetramino`initialSetMinoes(game, [1, 6]);
			return self
		);
			
		public getNextMino: Board`Position * nat ==> Board`Position
		getNextMino(position, index) == (
			dcl result : Board`Position := position;
			cases Tetramino`getOrientation():
				0 -> (
					cases index:
						2 -> result(1) := position(1) + 1,
						others -> result(2) := position(2) - 1
					end
				),
				1 -> (
					cases index:
						2 -> result(2) := position(2) - 1,
						others -> result(1) := position(1) - 1
					end
				),
				2 -> (
					cases index:
						2 -> result(1) := position(1) - 1,
						others -> result(2) := position(2) + 1
					end
				),
				3 -> (
					cases index:
						2 -> result(2) := position(2) + 1,
						others -> result(1) := position(1) + 1
					end
				)
			end;
			return result
		);	
		
		public getRotatedMino: Board`Position ==> Board`Position
		getRotatedMino(position) == (
			dcl result : Board`Position := position;
			cases Tetramino`getOrientation():
				0 -> result(1) := position(1) + 2,
				1 -> result(2) := position(2) - 2,
				2 -> result(1) := position(1) - 2,
				3 -> result(2) := position(2) + 2
			end;
			return result
		);	

end TetraminoS